plugins {
    id 'application'
    id 'checkstyle'
    id 'jacoco'
    id 'com.adarshr.test-logger' version '3.0.0'
    id 'com.github.ben-manes.versions' version '0.39.0'
    id 'io.ebean' version '13.6.5'
}

group 'hexlet.code'
version '1.0-SNAPSHOT'

application {
    mainClass = 'hexlet.code.App'
}

compileJava {
    options.release = 17
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testImplementation 'org.assertj:assertj-core:3.21.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'

    implementation 'io.javalin:javalin:5.3.1'
    implementation 'org.slf4j:slf4j-simple:2.0.6'
    implementation 'io.ebean:ebean:13.6.5'
    implementation 'io.ebean:ebean-ddl-generator:13.6.5'
    implementation 'io.ebean:ebean-querybean:13.6.5'
    implementation 'io.ebean:ebean-migration:13.6.2'
    implementation 'io.ebean:ebean-annotation:8.3'
    annotationProcessor 'io.ebean:querybean-generator:13.6.5'

    implementation 'org.glassfish.jaxb:jaxb-runtime:2.3.5'
    implementation 'javax.activation:activation:1.1.1'

    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'

    testCompileOnly 'org.projectlombok:lombok:1.18.24'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'
}

test {
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
        xml.required = false
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
}

checkstyle {
    toolVersion '9.0'
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

task generateMigrations(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'hexlet.code.GenerateDbMigration'
}

task stage(dependsOn: ['clean', 'installDist'])
installDist.mustRunAfter clean
