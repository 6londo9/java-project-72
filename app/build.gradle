plugins {
    id 'application'
    id 'checkstyle'
    id 'jacoco'
    id 'com.adarshr.test-logger' version '3.0.0'
    id 'com.github.ben-manes.versions' version '0.39.0'
    id 'io.ebean' version '13.6.5'
}

group 'hexlet.code'
version '1.0-SNAPSHOT'

application {
    mainClass = 'hexlet.code.App'
}

compileJava {
    options.release = 17
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'io.javalin:javalin:4.3.0'
    implementation 'org.slf4j:slf4j-simple:1.7.36'

    implementation 'org.thymeleaf:thymeleaf:3.0.15.RELEASE'
    implementation 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect:3.1.0'
    implementation 'org.thymeleaf.extras:thymeleaf-extras-java8time:3.0.4.RELEASE'
    implementation 'org.webjars:bootstrap:5.1.3'

    implementation 'com.h2database:h2:2.1.210'
    implementation 'org.postgresql:postgresql:42.3.3'

    implementation 'io.ebean:ebean:12.15.1'
    implementation 'io.ebean:ebean-ddl-generator:12.15.1'
    implementation 'io.ebean:ebean-querybean:12.15.1'
    implementation 'io.ebean:ebean-migration:12.15.1'
    implementation 'io.ebean:ebean-annotation:7.7'
    annotationProcessor 'io.ebean:querybean-generator:12.15.1'

    implementation 'org.glassfish.jaxb:jaxb-runtime:2.3.5'
    implementation 'javax.activation:activation:1.1.1'

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.8.2'
    testImplementation 'org.assertj:assertj-core:3.22.0'
    testImplementation 'io.ebean:ebean-test:12.15.1'

    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'

    testCompileOnly 'org.projectlombok:lombok:1.18.24'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'
}

test {
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
        xml.required = false
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
}

checkstyle {
    toolVersion '9.0'
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

task generateMigrations(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'hexlet.code.GenerateDbMigration'
}

task stage(dependsOn: ['clean', 'installDist'])
installDist.mustRunAfter clean
